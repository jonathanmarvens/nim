use chimpunit;

main argv {
  chimpunit.test("int array comparison", fn { |t|
    t.equals([1, 2, 3], [1, 2, 3]);
    t.not_equals([1, 2, 3], [3, 2, 1]);
    t.not_equals([1, 2, 3], [1, 2, 3, 4]);
  });

  chimpunit.test("string array comparison", fn { |t|
    t.equals(["a", "b", "c"], ["a", "b", "c"]);
    t.not_equals(["a", "b", "c"], ["c", "b", "a"]);
    t.not_equals(["a", "b", "c"], ["a", "b", "c", "d"]);
  });

  chimpunit.test("mismatched comparison", fn { |t|
    t.not_equals([1], ["1"]);
  });

  chimpunit.test("contains", fn { |t|
    t.equals(true,  [1, 2, 3].contains(2));
    t.equals(false, [1, 2, 3].contains(5));
  });

  chimpunit.test("map", fn { |t|
    var result = [1, 2, 3].map(str);
    t.equals(["1", "2", "3"], result);
  });

  chimpunit.test("each", fn { |t|
    var sum = 0;
    [1, 2, 3].each(fn { |i| sum = sum + i; });
    t.equals(6, sum);
  });

  chimpunit.test("filter", fn { |t|
    var result = [1, 2, 3].filter(fn { |i| i >= 2; });
    t.equals([2, 3], result);
  });

  chimpunit.test("join", fn { |t|
    var result = ["1", "2", "3"].join(",");
    t.equals("1,2,3", result);
  });

  chimpunit.test("concatenation with `+`", fn { |t|
    var result = [1, 2, 3] + [4, 5, 6];
    t.equals(result, [1, 2, 3, 4, 5, 6]);
  });

  chimpunit.test("remove element", fn { |t|
    var items = ["a", "b", "c"];
    t.equals(items.remove("b"), true);
    t.equals(items, ["a", "c"]);
    t.equals(items.remove("c"), true);
    t.equals(items, ["a"]);
    t.equals(items.remove(nil), false);
    t.equals(items.remove("a"), true);
    t.equals(items, []);
  });

  chimpunit.test("remove element at index", fn { |t|
    var items = [1, 2, 3];
    t.equals(items.remove_at(0), 1);
    t.equals(items.remove_at(1), 3);
    t.equals(items.remove_at(0), 2);
  });
}
